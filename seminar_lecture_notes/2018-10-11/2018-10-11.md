# 讲义2018-10-11

Author: 吕栋杰(huisedenanhai)

# 点名
# 服务器与gdb的使用

+ `ssh -p port usr@host`
+ `sftp -P port usr@host` 注意这两条指令的大小写
+ \*ssh config
+ gdb
    + `run [args]` 运行程序
    + `Ctrl+C` 停止运行
    + `b` 添加断点
        + `b 函数名` 给函数加断点
        + `b *addr` 在某个地址加断点
    + `i b` 查看断点信息
    + `i reg` 查看寄存器信息
    + `x/nfu addr` 查看内存
        + `n` 显示个数
        + `f` 显示格式，比如`x`代表用16进制表示
        + `u` 数的大小，`w`是四字节，`g`是八字节
    + `bt` 显示堆栈追踪
    + `si` or `stepi` 单步调试
    + `c` or `continue` 继续执行
    + `fin` or `finish` 继续执行直至函数结束
    + `layout asm` 显示汇编
    + `tui disable` 把ui关掉

# 例题

1. 判断下面指令的合法性，机器使用x86-64

```assembly
movq 123, (%rsi)             ; 1
movw %bp, %sp                ; 2
movl %rbx, %eax              ; 3
movq (%rsp, ,4), %rdx        ; 4
movl %ebx, (%eax, %edx)      ; 5
leaq $4(%rsp, %rax, 4), %rax ; 6
sarq %ebx, %rax              ; 7
shrl %cl, %eax               ; 8
shlb $2, %ah                 ; 9
jne *.L1(%rax, %rbx, 8)      ; 10
```

<!--
1. 错，不能从内存移到内存
2. 对
3. 错，寄存器大小需要和指令匹配
4. 错，寻址格式错误
5. 错，64位机器上基址和变址寄存器都必须是64位
6. 错，寻址格式错误，不应该有$
7. 错，移位指令第一个操作数必须是立即数或%cl
8. 对
9. 对，认识一下%ah这个寄存器
10. 错，条件跳转不能间接跳转
-->

2.

函数`foo`的声明如下
```c
long foo(long v);
```

对应汇编代码如下

```assembly
foo:
  movl %edi, %eax
  cltq
  sarq $0x20, %rdi
  notq %rax
  leaq 1(%rax, %rdi), %rax
```

问`foo(-2)`的返回值为？

<!--
功能与下方程序等价
```c
long foo(long v) {
  int vl = v;
  int vh = v >> 32;
  return (long)vh - (long)vl;
}
```
foo(-2) = 1;
-->

3. 下面的指令分别会设置哪些条件码

| INS   | CF  | ZF  | SF  | OF  |
| ----- | --- | --- | --- | --- |
| leaq  |     |     |     |     |
| add   |     |     |     |     |
| cmp   |     |     |     |     |
| xor   |     |     |     |     |
| test  |     |     |     |     |
| shr   |     |     |     |     |
| inc   |     |     |     |     |
| sete  |     |     |     |     |
| jne   |     |     |     |     |
| cmove |     |     |     |     |


<!--
| INS   | CF               | ZF  | SF  | OF  |
| ----- | ---------------- | --- | --- | --- |
| leaq  | -                | -   | -   | -   |
| add   | Y                | Y   | Y   | Y   |
| cmp   | Y                | Y   | Y   | Y   |
| xor   | 0                | Y   | Y   | 0   |
| test  | 0                | Y   | Y   | 0   |
| shr   | last removed bit | Y   | Y   | Y   |
| inc   | -                | Y   | Y   | Y   |
| sete  | -                | -   | -   | -   |
| jne   | -                | -   | -   | -   |
| cmove | -                | -   | -   | -   |
+ lea不改变条件码
+ 所有逻辑操作都会把CF和OF设为0，包括test
+ inc和dec指令不改变CF
+ 移位操作会把CF设为最后一个被移出的位（无论左移还是右移）
> Shifts the bits in the first operand (destination operand) to the left or right by the number of bits specified in the second operand (count operand). Bits shifted beyond the destination operand boundary are first shifted into the CF flag, then discarded. At the end of the shift operation, the CF flag contains the last bit shifted out of the destination operand.
+ 移位操作对于OF的行为，在CS:APP(3e, 3eZN)上的说法与Intel手册不同，Intel手册给出的规则略复杂，下面给出，仅供参考：
> 对于移位操作，溢出标志设置为0。 (CS:APP 3eZN p. 136)

vs.

> The OF flag is affected only on 1-bit shifts. For left shifts, the OF flag is set to 0 if the most-significant bit of the result is the same as the CF flag (that is, the top two bits of the original operand were the same); otherwise, it is set to 1. For the SAR instruction, the OF flag is cleared for all 1-bit shifts. For the SHR instruction, the OF flag is set to the most-significant bit of the original operand. (Intel ASDM vol. 2B Chapter 4.3 p.2B-4-581)
-->

4. 某程序中，一段switch语句编译后得到如下结果

```assembly
addl	$-0x31, %edi
cmpl	$8, %edi
ja	LBB0_6
leaq	LJTI0_0(%rip), %rcx
movslq	(%rcx,%rdi,4), %rdx
addq	%rcx, %rdx
jmpq	*%rdx
```

```assembly
L0_0_set_2 = LBB0_2-LJTI0_0
L0_0_set_6 = LBB0_6-LJTI0_0
L0_0_set_3 = LBB0_3-LJTI0_0
L0_0_set_4 = LBB0_4-LJTI0_0
L0_0_set_5 = LBB0_5-LJTI0_0
LJTI0_0:
  .long	L0_0_set_2
  .long	L0_0_set_6
  .long	L0_0_set_3
  .long	L0_0_set_3
  .long	L0_0_set_4
  .long	L0_0_set_6
  .long	L0_0_set_6
  .long	L0_0_set_6
  .long	L0_0_set_5
  .end_data_region
```

求switch中出现的所有label

hint: 字符`0`的ascii码为`0x30`

<!--
从ja可知default对应LBB0_6，那么其他的都是switch语句中出现的符号

符号是：`'1'`, `'3'`,`'4'`,`'5'`,`'9'`
-->

5. 阅读下方汇编代码，回答问题

```assembly
	.section	__TEXT,__text,regular,pure_instructions
	.macosx_version_min 10, 13
	.globl	_show_maze              ## -- Begin function show_maze
	.p2align	4, 0x90
_show_maze:                             ## @show_maze
	.cfi_startproc
## BB#0:
	pushq	%rbp
Lcfi0:
	.cfi_def_cfa_offset 16
Lcfi1:
	.cfi_offset %rbp, -16
	movq	%rsp, %rbp
Lcfi2:
	.cfi_def_cfa_register %rbp
	pushq	%r15
	pushq	____
	pushq	%r13
	pushq	%r12
	pushq	%rbx
	pushq	%rax
Lcfi3:
	.cfi_offset %rbx, -56
Lcfi4:
	.cfi_offset %r12, -48
Lcfi5:
	.cfi_offset %r13, -40
Lcfi6:
	.cfi_offset %r14, -32
Lcfi7:
	.cfi_offset %r15, -24
	____	%r13d, %r13d
	movq	_playerI@GOTPCREL(%rip), %r14
	movq	_playerJ@GOTPCREL(%rip), %r15
	leaq	_maze(%rip), %r12
	.p2align	4, 0x90
LBB0_1:                                 ## =>This Loop Header: Depth=1
                                        ##     Child Loop BB0_2 Depth 2
	xorl	%ebx, %ebx
	.p2align	4, 0x90
LBB0_2:                                 ##   Parent Loop BB0_1 Depth=1
                                        ## =>  This Inner Loop Header: Depth=2
	movl	(%r14), %eax
	cmpq	%rax, %r13
	jne	LBB0_7
## BB#3:                                ##   in Loop: Header=BB0_2 Depth=2
	movl	(%r15), %eax
	cmpq	%rax, %rbx
	jne	LBB0_7
## BB#4:                                ##   in Loop: Header=BB0_2 Depth=2
	movl	$42, %edi
	jmp	LBB0_8
	.p2align	4, 0x90
LBB0_7:                                 ##   in Loop: Header=BB0_2 Depth=2
	movq	(%r12,%r13,___), %rax
	movsbl	(%rax,%rbx), ____
LBB0_8:                                 ##   in Loop: Header=BB0_2 Depth=2
	____	_putchar
	incq	%rbx
	cmpq	$6, %rbx
	jne	LBB0_2
## BB#5:                                ##   in Loop: Header=BB0_1 Depth=1
	movl	$10, %edi
	callq	_putchar
	incq	%r13
	cmpq	$6, %r13
	jne	LBB0_1
## BB#6:
	addq	$8, %rsp
	popq	%rbx
	popq	%r12
	popq	____
	popq	%r14
	popq	%r15
	popq	%rbp
	retq
	.cfi_endproc
                                        ## -- End function
	.globl	_main                   ## -- Begin function main
	.p2align	4, 0x90
_main:                                  ## @main
	.cfi_startproc
## BB#0:
	pushq	%rbp
Lcfi8:
	.cfi_def_cfa_offset 16
Lcfi9:
	.cfi_offset %rbp, -16
	movq	%rsp, %rbp
Lcfi10:
	.cfi_def_cfa_register %rbp
	movl	_startI(%rip), %eax
	movq	_playerI@GOTPCREL(%rip), %rcx
	movl	%eax, (%rcx)
	movl	_startJ(%rip), %eax
	movq	_playerJ@GOTPCREL(%rip), %rcx
	movl	%eax, (%rcx)
	callq	_show_maze
	xorl	%eax, %eax
	popq	%rbp
	retq
	.cfi_endproc
                                        ## -- End function
	.section	__TEXT,__cstring,cstring_literals
L_.str:                                 ## @.str
	.asciz	"######"

L_.str.1:                               ## @.str.1
	.asciz	"     #"

L_.str.2:                               ## @.str.2
	.asciz	"#### #"

	.section	__DATA,__data
	.globl	_maze                   ## @maze
	.p2align	4
_maze:
	.quad	L_.str
	.quad	L_.str.1
	.quad	L_.str.2
	.quad	L_.str.2
	.quad	L_.str.1
	.quad	L_.str

	.globl	_startI                 ## @startI
	.p2align	2
_startI:
	.long	1                       ## 0x1

	.globl	_startJ                 ## @startJ
.zerofill __DATA,__common,_startJ,4,2
	.globl	_endI                   ## @endI
	.p2align	2
_endI:
	.long	4                       ## 0x4

	.globl	_endJ                   ## @endJ
.zerofill __DATA,__common,_endJ,4,2
	.comm	_playerI,4,2            ## @playerI
	.comm	_playerJ,4,2            ## @playerJ

.subsections_via_symbols
```

+ 填补程序中空缺的部分
+ 这个程序运行的输出是什么（hint：换行符的ascii码是10）

<!--
见[q5.c](./src/q5.c)与[q5.s](./src/q5.s)
-->
